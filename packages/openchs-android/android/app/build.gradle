apply plugin: "com.android.application"
apply plugin: 'com.google.gms.google-services'

googleServices {
    disableVersionCheck = true
}

import com.android.build.OutputFile

def enableHermes = System.getenv("enableHermes") ? System.getenv("enableHermes").toBoolean() : false;
println('[Avni] Hermes Enabled:' + enableHermes)

project.ext.react = [
        extraPackagerArgs: ["--sourcemap-output", "$buildDir/generated/sourcemap.js"],
        enableHermes: enableHermes
]

apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = System.getenv("enableSeparateBuildPerCPUArchitecture") ? System.getenv("enableSeparateBuildPerCPUArchitecture").toBoolean() : true

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = true


android {
    ndkVersion rootProject.ext.ndkVersion

    dexOptions {
        javaMaxHeapSize "3g"
    }

    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode System.getenv("versionCode") ? 4 * 1048576 + System.getenv("versionCode").toInteger() : 1
        //Our convention of versionCode from now in is to form the number from digits in versionName. E.g. for versionName 2.6.1, the versionCode should be 261. This is to avoid conflicts of versionCodes between different branches.
        // Major.Minor.Patch. We should not change major version every release, just change the minor. Each number allows for two digits
        // https://medium.com/@maxirosson/versioning-android-apps-d6ec171cfd82
        versionName System.getenv("versionName") ? System.getenv("versionName") : "1"

        ndk {
            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
            if (enableSeparateBuildPerCPUArchitecture) {
                abiFilters.clear()
            }
        }
        multiDexEnabled true
    }

    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
            universalApk false
            // If true, also generate a universal APK. This does not affect generation of separate apk per cpu architecture. This is an add on apk that is generated for upload to other markets, such as APKFiles, which do not support multiple APKs for a single app.

        }
    }

    signingConfigs {

        generic {

            storeFile file("${System.getenv("KEY_STORE_PREFIX") ?: ""}openchs-release-key.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias "${System.getenv("KEY_STORE_PREFIX") ?: ""}openchs-release-key"
            keyPassword System.getenv("KEY_PASSWORD")
        }
        lfe {
            storeFile file("${System.getenv("KEY_STORE_PREFIX") ?: ""}lfeteach-avni-release-key.keystore")
            storePassword System.getenv("LFE_KEYSTORE_PASSWORD")
            keyAlias "${System.getenv("KEY_STORE_PREFIX") ?: ""}openchs-release-key"
            keyPassword System.getenv("LFE_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "organisation"
    productFlavors {
        generic {
            applicationId "com.openchsclient"
            resValue "string", "app_name", "Avni"
            signingConfig signingConfigs.generic
            manifestPlaceholders = [bugsnagAPIKey: System.getenv("OPENCHS_CLIENT_BUGSNAG_API_KEY") ?: "dummy"]
        }
        lfe {
            applicationId "org.lfeteach.openchsclient"
            resValue "string", "app_name", "Teach AP"
            signingConfig signingConfigs.lfe
            manifestPlaceholders = [bugsnagAPIKey: System.getenv("LFE_CLIENT_BUGSNAG_API_KEY") ?: "dummy"]
        }
    }
}

dependencies {
    implementation project(':react-native-background-timer')
    implementation project(':react-native-file-viewer')
    implementation project(':react-native-keep-awake')
    implementation project(':react-native-audio-recorder-player')
    implementation project(':react-native-document-picker')
    implementation project(':react-native-zip-archive')
    implementation project(':react-native-webview')
    implementation project(':@react-native-firebase_app')
    implementation project(':@react-native-firebase_analytics')
    implementation project(':react-native-immediate-phone-call')
    implementation project(':amazon-cognito-identity-js')
    implementation project(':rn-fetch-blob')
    implementation project(':react-native-image-picker')
    implementation project(':react-native-fs')
    implementation(project(':react-native-video')) {
        exclude group: 'com.squareup.okhttp3'
    }
    implementation project(':bugsnag-react-native')
    implementation project(':react-native-geolocation-service')
    implementation project(':react-native-charts-wrapper')
    implementation project(':react-native-vector-icons')
//    implementation project(':react-native-i18n')
//    implementation project(':realm')
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:${rootProject.ext.supportLibVersion}"
    implementation project(':react-native-device-info')
    implementation "com.facebook.react:react-native:+"
    implementation 'com.facebook.soloader:soloader:0.10.5'
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha03'

    // required to avoid crash on Android 12 API 31
    implementation 'androidx.work:work-runtime:2.7.1'

    if (enableHermes) {
        implementation("com.facebook.react:hermes-engine:+") { // From node_modules
            exclude group:'com.facebook.fbjni'
        }
    } else {
        implementation 'org.webkit:android-jsc:+'
    }

    debugImplementation('com.facebook.flipper:flipper:0.174.0') {
        exclude group:'com.facebook.fbjni'
    }

    debugImplementation('com.facebook.flipper:flipper-network-plugin:0.174.0') {
        exclude group:'com.facebook.flipper'
    }
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.implementation
    into 'libs'
}

apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)
